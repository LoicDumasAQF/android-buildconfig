// see https://github.com/shyiko/ktlint#-with-gradle
repositories {
    mavenCentral()
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.14.0"
}

task ktlint(
        type: JavaExec,
        group: 'verification',
        description: 'Check Kotlin code style.') {
    classpath = configurations.ktlint
    main = 'com.github.shyiko.ktlint.Main'
    // see https://github.com/shyiko/ktlint#usage
    args    '--reporter=plain',
            "--reporter=checkstyle,output=${buildDir}/reports/ktlint.xml",
            'src/**/*.kt'
}
check.dependsOn ktlint

task ktlintFormat(
        type: JavaExec,
        group: 'formatting',
        description: 'Fix Kotlin code style deviations.') {
    classpath = configurations.ktlint
    main = 'com.github.shyiko.ktlint.Main'
    args '-F', 'src/**/*.kt'
}
